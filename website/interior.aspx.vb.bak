Imports System
Imports System.Data
Imports System.Data.SqlClient

Partial Class Interior
    Inherits RichTemplateLanguagePage

    Dim boolWebpage_PublicSection_EnableGroupsAndUsers As Boolean = False
    Dim intWebInfoID_Header As Integer = Integer.MinValue
    Dim intWebInfoID_Footer As Integer = Integer.MinValue

    Dim intSiteID As Integer = Integer.MinValue
    Dim intMemberID As Integer = Integer.MinValue

    Dim _PageID As Integer = 0
    Public ReadOnly Property PageID() As Integer
        Get
            If _PageID = 0 Then
                _PageID = CommonWeb.GetCurrentPageID(intWebInfoID_Header, intWebInfoID_Footer, False, False)
            End If

            Return _PageID
        End Get
    End Property

    Protected Sub Page_Load(ByVal sender As Object, ByVal e As EventArgs) Handles MyBase.Load

        'First we set the SiteID and MemberID
        intSiteID = SiteDAL.GetCurrentSiteID_FrontEnd()
        intMemberID = MemberDAL.GetCurrentMemberID()

        If Not Page.IsPostBack Then

            'Check Access - ALSO THIS MUST BE THE FIRST THING WE DO, as we set both the intWebInfoID_Header and intWebInfoID_Footer here, which we would use when calling the PageID property
            Dim dtSite As DataTable = SiteDAL.GetSite_ByID(intSiteID)
            If dtSite.Rows.Count > 0 Then
                Dim drSite As DataRow = dtSite.Rows(0)
                boolWebpage_PublicSection_EnableGroupsAndUsers = Convert.ToBoolean(drSite("Webpage_PublicSection_EnableGroupsAndUsers"))
                intWebInfoID_Header = Convert.ToInt32(drSite("WebInfoID_Header"))
                intWebInfoID_Footer = Convert.ToInt32(drSite("WebInfoID_Footer"))
            End If

            Page.Header.Title = SiteDAL.GetCompanyName() & " | " & Resources.WebInfo_FrontEnd.Interior_Public_HtmlTitleDefault
            DisplayMainContent()

        End If

        'Setup our Comments for WebInfo Pages - NOTE We allow public (not logged in) users to submit comments
        'ucCommentsWebInfo.SetupCommentWebInfo(PageID, intSiteID, intMemberID, True)

    End Sub

    Protected Sub DisplayMainContent()


        Dim dtWebInfo As DataTable = If(boolWebpage_PublicSection_EnableGroupsAndUsers, WebInfoDAL.GetWebInfo_ByIDAndAccess_FrontEnd(PageID, MemberDAL.GetCurrentMemberGroupIDs, intMemberID), WebInfoDAL.GetWebInfo_ByID_FrontEnd(PageID))
        If dtWebInfo.Rows.Count > 0 Then
            Dim drWebInfo As DataRow = dtWebInfo.Rows(0)

            Dim strPageName As String = drWebInfo("Name").ToString()

            'set the Page Header
            CommonWeb.SetMasterPageBannerText(Me.Master, strPageName)

            'set the page content
            If drWebInfo("message") Is DBNull.Value Then
                mainContent.Text = Resources.WebInfo_FrontEnd.Interior_Public_PageNotPublished
            Else
                mainContent.Text = drWebInfo("message").ToString()
            End If

            'set the page title
            Page.Header.Title = SiteDAL.GetCompanyName() & " | " & strPageName
            If Not drWebInfo("metaTitle") Is DBNull.Value Then
                Dim strMetaTitle As String = drWebInfo("metaTitle").ToString()
                If strMetaTitle.Length > 0 Then
                    Page.Header.Title = strMetaTitle
                End If
            End If

            'set the page's keywords
            If Not drWebInfo("metaKeyword") Is DBNull.Value Then
                Dim keywordsHtmlMeta As New HtmlMeta()
                keywordsHtmlMeta.Name = "keywords"
                keywordsHtmlMeta.Content = drWebInfo("metaKeyword").ToString()
                ' Add the HtmlMeta object to the page header's controls. 
                Page.Header.Controls.Add(keywordsHtmlMeta)
            End If

            'set the page's description
            If Not drWebInfo("metaDesc") Is DBNull.Value Then
                Dim descriptionHtmlMeta As New HtmlMeta()
                descriptionHtmlMeta.Name = "description"
                descriptionHtmlMeta.Content = drWebInfo("metaDesc").ToString()
                ' Add the HtmlMeta object to the page header's controls. 
                Page.Header.Controls.Add(descriptionHtmlMeta)
            End If
        Else
            Response.Redirect("/404.aspx")
        End If

    End Sub

End Class
